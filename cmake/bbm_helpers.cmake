#########################################################################
# BBM Cmake Helpers
#########################################################################

#########################################################################
# get_backbone_list: get the list of available backbones
#########################################################################
macro(get_backbone_list RESULT)
  set(${RESULT})
  file(GLOB BACKBONE_DIRS backbone/*)
  foreach(BDIR ${BACKBONE_DIRS})
    get_filename_component(NAME ${BDIR} NAME)
    list(APPEND ${RESULT} ${NAME})
  endforeach()
endmacro()


#########################################################################
# setup_backbone: set up the backbone
#########################################################################
macro(setup_backbone)
  if(NOT BBM_BACKBONE)
    set(BBM_BACKBONE "native")
    get_backbone_list(BACKBONE_LIST)
    message(STATUS "BBM_BACKBONE not set; using default 'native' backbone (available backbones are: ${BACKBONE_LIST}).")
  endif()
  
  if(NOT EXISTS ${BBM_SOURCE_DIR}/backbone/${BBM_BACKBONE})
    get_backbone_list(BACKBONE_LIST)
    message(FATAL_ERROR "Invalid backbone '${BBM_BACKBONE}'; set BBM_BACKBONE to one of: ${BACKBONE_LIST}.")
    return()
  endif()

  if(NOT EXISTS ${BBM_SOURCE_DIR}/backbone/${BBM_BACKBONE}/backbone.cmake )
    message(FATAL_ERROR "Backbone does not contain a valid 'backbone.cmake'")
    return()
  endif()

  include( ${BBM_SOURCE_DIR}/backbone/${BBM_BACKBONE}/backbone.cmake )

endmacro()


#########################################################################
# write_bsdfmodel header: generate bbm_bsdfmodels.h containing includes 
#                         to all header files in the ${MODEL_DIRS}
#########################################################################
function(write_bsdfmodel_header BASE_DIR MODEL_DIRS)
  set(FNAME "${BASE_DIR}/bbm_bsdfmodels.h")
  message(STATUS "Generating '${FNAME}'")

  file(WRITE  ${FNAME} "/***********************************************************************/\r\n")
  file(APPEND ${FNAME} "/*! \\file bbm_bsdfmodels.h\r\n")
  file(APPEND ${FNAME} "    \\brief Include all bsdfmodels' header files. Autogenerated by cmake.\r\n\r\n")
  file(APPEND ${FNAME} "************************************************************************/\r\n\r\n")

  foreach(MODELS ${MODEL_DIRS})
    file(GLOB H_FILES ${BASE_DIR}/${MODELS})

    foreach(H_FILE ${H_FILES})
       get_filename_component(NAME ${H_FILE} NAME)
       get_filename_component(DIR ${MODELS} DIRECTORY)
       file(APPEND ${FNAME} "#include \"${DIR}/${NAME}\"\r\n")
    endforeach()
    
  endforeach()

endfunction()

